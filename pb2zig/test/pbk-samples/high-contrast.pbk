<languageVersion : 1.0;>

kernel HighContrastFilter
<   namespace : "com.mimswright";
    vendor : "MimsWright.com";
    version : 1;
    description : "extreme contrast filter that converts to either 1 or 0 based on brightness of source.";
>
{
    input image4 src;
    output pixel4 dst;
    
    parameter float threshhold
    <minValue: 0.0;
     maxValue: 255.0;
     defaultValue: 128.0;>;

    parameter int invert
    <minValue: 0; maxValue:1; defaultValue: 0;>;

    void
    evaluatePixel()
    {
        pixel4 px = sampleNearest(src, outCoord());
        float brightest = max( px.r, max( px.g, px.b));  
        float v;
        if ( invert > 0 ) {
            v = step(threshhold, (brightest * 255.0));
        } else {
            v = step((brightest * 255.0), threshhold);
        }
        dst = pixel4(v,v,v,v);
    }
}
